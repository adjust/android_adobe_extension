plugins {
    id 'com.android.library'
    id 'com.jfrog.bintray'
    id 'maven-publish'
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 30
        versionCode 1
        versionName '0.0.1'

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation 'com.adjust.sdk:adjust-android:4.24.1'
    implementation 'com.adobe.marketing.mobile:sdk-core:1.5.7'
}

//region Publish to jCenter
def libGroupId = 'com.adjust.adobeextension'
def libArtifactId = 'adobeextension'
def libVersion = '0.0.1'
def libDesc = 'This is the Android Adobe Mobile Extension of Adjust.'
def libLicenses = ['Apache-2.0']
def libVCSUrl = 'https://github.com/adjust/android_adobe_extension.git'

// Generate Source Jar
task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

// Create the publication with the pom configuration
publishing {
    publications {
        AdobeExtensionAar(MavenPublication) {
            groupId libGroupId
            artifactId libArtifactId
            version libVersion
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/$libArtifactId-release.aar")
        }
    }
}

// Bintray config
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications = ['AdobeExtensionAar']
    pkg {
        repo = "Maven"
        name = "$libGroupId:$libArtifactId"
        desc = libDesc
        licenses = libLicenses
        vcsUrl = libVCSUrl
        version {
            name = libVersion
            vcsTag = libVersion
            released = new Date()
        }
    }
}


