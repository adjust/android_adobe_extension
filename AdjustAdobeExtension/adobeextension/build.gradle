apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    namespace 'com.adjust.adobeextension'
    compileSdk 34

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 34

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation 'androidx.test.ext:junit:1.1.5'
    implementation 'com.adjust.sdk:adjust-android:4.38.1'
    implementation 'com.adobe.marketing.mobile:core:2.6.1'
}

// init
def libGroupId = 'com.adjust.adobeextension'
def libArtifactId = 'adobeextension'
def libVersion = '2.0.0'
def libName = 'Adjust Extension for Adobe Experience SDK'
def libDesc = 'This is the Android Adobe Mobile Extension of Adjust.'
def libUrl = 'https://github.com/adjust/android_adobe_extension'

// read local properties
File localPropsFile = project.rootProject.file('local.properties')
if (localPropsFile.exists()) {
    Properties p = new Properties()
    new FileInputStream(localPropsFile).withCloseable { is ->
        p.load(is)
    }
    p.each { name, value ->
        ext[name] = value
    }
}

// Generate Source Jar
task sourceJar(type: Jar) {
    setArchiveClassifier('sources')
    from android.sourceSets.main.java.srcDirs
}

// Create the publication with the pom configuration
def pomConfig = {
    organization {
        name 'adjust GmbH'
        url 'http://www.adjust.com'
    }
    licenses {
        license {
            name 'MIT License'
            url 'http://www.opensource.org/licenses/mit-license.php'
        }
    }
    scm {
        url 'git@github.com:adjust/android_adobe_extension.git'
        connection 'scm:git:git@github.com:adjust/android_adobe_extension.git'
        developerConnection 'scm:git:git@github.com:adjust/android_adobe_extension.git'
    }
    developers {
        developer {
            name 'Pedro Silva'
            email 'pedro@adjust.com'
        }
        developer {
            name 'Shashank Suhane'
            email 'shashank@adjust.com'
        }
        developer {
            name 'Ugljesa Erceg'
            email 'ugljesa@adjust.com'
        }
    }
}

publishing {
    publications {
        AdobeExtensionAar(MavenPublication) {
            groupId libGroupId
            artifactId libArtifactId
            version libVersion
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/$libArtifactId-release.aar")
            pom.withXml {
                def root = asNode()
                root.appendNode('description', libDesc)
                root.appendNode('name', libName)
                root.appendNode('url', libUrl)
                root.children().last() + pomConfig
            }
        }
    }

    repositories {
        maven {
            name = "mavencentral"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications
}
